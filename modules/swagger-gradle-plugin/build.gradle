// * * * * * * * * * * * *
// Plugins
// * * * * * * * * * * * *
plugins {
    id 'java-gradle-plugin'
    id 'net.researchgate.release' version '2.6.0'
    // Publishing publicly
    id 'com.gradle.plugin-publish' version '0.11.0'
    // Publishing to maven
    id 'maven-publish'
}

// * * * * * * * * * * * *
// Dependencies
// * * * * * * * * * * * *

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-lang3:3.7'

    testImplementation gradleTestKit()
    testImplementation 'com.github.tomakehurst:wiremock:2.27.2'
    testImplementation 'javax.servlet:javax.servlet-api:3.1.0'
    testImplementation 'com.google.guava:guava:27.0.1-jre'
    testImplementation 'javax.ws.rs:javax.ws.rs-api:2.1'
    testImplementation "io.swagger.core.v3:swagger-jaxrs2:${project.version}"
    testImplementation 'junit:junit:4+'
    testImplementation "org.eclipse.jetty:jetty-server:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-servlet:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-servlets:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-webapp:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-proxy:${project.jettyVersion}"
    testImplementation "org.apache.httpcomponents:httpclient:4.5.13"
    testImplementation "commons-codec:commons-codec:1.15"
    testImplementation "org.apache.commons:commons-compress:1.20"

    testImplementation('com.github.tomakehurst:wiremock:2.27.2') {
        exclude group: 'org.eclipse.jetty', module: 'jetty-server'
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlets'
        exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
        exclude group: 'org.eclipse.jetty', module: 'jetty-proxy'
        exclude group: 'commons-codec', module: 'commons-codec'
    }
}

// * * * * * * * * * * * *
// Project configuration
// * * * * * * * * * * * *

project.description     = 'Gradle Plugin for Swagger core'
project.group           = 'io.swagger.core.v3'

project.ext.name        = 'swagger-gradle-plugin'
project.ext.pluginId    = 'io.swagger.core.v3.swagger-gradle-plugin'
project.ext.scm         = 'https://github.com/swagger-api/swagger-core.git'
project.ext.url         = 'https://github.com/swagger-api/swagger-core/tree/master/modules/swagger-gradle-plugin'

// Configuration for: java-gradle-plugin
gradlePlugin {
    plugins {
        swagger {
            id = project.pluginId
            implementationClass = 'io.swagger.v3.plugins.gradle.SwaggerPlugin'
        }
    }
}

// * * * * * * * * * * * *
// Publishing
// * * * * * * * * * * * *


// Publishing to plugins.gradle.org
// --------------------------------

// Configuration for: com.gradle.plugin-publish
pluginBundle {
    website = project.url
    vcsUrl = project.scm
    tags = ['swagger', 'openapi', 'api']

    plugins {
        swagger {
            displayName = 'Swagger Gradle Plugin '
            description = project.description
            id          = project.pluginId
        }
    }
}

publishing {
    repositories {
        maven {
            url mavenLocal().url
        }
    }
}
