// * * * * * * * * * * * *
// Plugins
// * * * * * * * * * * * *
plugins {
    id 'java-gradle-plugin'
    // Publishing publicly
    id 'com.gradle.plugin-publish' version '0.21.0'
    // Publishing to maven
    id 'maven-publish'
}

// * * * * * * * * * * * *
// Dependencies
// * * * * * * * * * * * *

repositories {
    mavenLocal()
    mavenCentral()
}

compileJava {
    options.release = 8
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-lang3:3.18.0'

    testImplementation gradleTestKit()
    testImplementation('com.github.tomakehurst:wiremock:2.27.2') {
        exclude group: 'com.jayway.jsonpath', module: 'json-path'
        exclude group: 'org.eclipse.jetty', module: 'jetty-server'
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlet'
        exclude group: 'org.eclipse.jetty', module: 'jetty-servlets'
        exclude group: 'org.eclipse.jetty', module: 'jetty-webapp'
        exclude group: 'org.eclipse.jetty', module: 'jetty-proxy'
        exclude group: 'commons-codec', module: 'commons-codec'
        exclude group: 'com.github.jknack', module: 'handlebars'
        exclude group: 'com.github.jknack', module: 'handlebars-helpers'
    }
    testImplementation 'javax.servlet:javax.servlet-api:3.1.0'
    testImplementation 'com.google.guava:guava:32.1.3-jre'
    testImplementation 'javax.ws.rs:javax.ws.rs-api:2.1.1'
    testImplementation "io.swagger.core.v3:swagger-jaxrs2:${project.version}"
    testImplementation 'org.testng:testng:7.10.2'
    testImplementation "org.eclipse.jetty:jetty-server:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-servlet:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-servlets:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-webapp:${project.jettyVersion}"
    testImplementation "org.eclipse.jetty:jetty-proxy:${project.jettyVersion}"
    testImplementation "org.apache.httpcomponents:httpclient:4.5.14"
    testImplementation "commons-codec:commons-codec:1.16.0"
    testImplementation "commons-io:commons-io:2.15.1"
    testImplementation "org.apache.commons:commons-compress:1.25.0"
    testImplementation "com.github.jknack:handlebars:4.3.1"
    testImplementation "com.github.jknack:handlebars-helpers:4.3.1"
}

// * * * * * * * * * * * *
// Project configuration
// * * * * * * * * * * * *

project.description     = 'Gradle Plugin for Swagger core'
project.group           = 'io.swagger.core.v3'

project.ext.name        = 'swagger-gradle-plugin'
project.ext.pluginId    = 'io.swagger.core.v3.swagger-gradle-plugin'
project.ext.scm         = 'https://github.com/swagger-api/swagger-core.git'
project.ext.url         = 'https://github.com/swagger-api/swagger-core/tree/master/modules/swagger-gradle-plugin'

// Configuration for: java-gradle-plugin
gradlePlugin {
    plugins {
        swagger {
            id = project.pluginId
            implementationClass = 'io.swagger.v3.plugins.gradle.SwaggerPlugin'
        }
    }
}

// * * * * * * * * * * * *
// Publishing
// * * * * * * * * * * * *


// Publishing to plugins.gradle.org
// --------------------------------

// Configuration for: com.gradle.plugin-publish
pluginBundle {
    website = project.url
    vcsUrl = project.scm
    tags = ['swagger', 'openapi', 'api']

    plugins {
        swagger {
            displayName = 'Swagger Gradle Plugin '
            description = project.description
            id          = project.pluginId
        }
    }
}

test {
    useTestNG()
}

publishing {
    repositories {
        maven {
            url mavenLocal().url
        }
    }
}
